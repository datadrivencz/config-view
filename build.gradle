plugins {
  id 'idea'
  id 'java'
  id 'jacoco'
  id 'java-library'
  id 'com.github.spotbugs' version '4.7.10'
  id "com.diffplug.spotless" version "6.0.0"
  id 'net.researchgate.release' version '2.8.1'
  id "org.sonarqube" version "3.3"
}

group = 'cz.datadriven.utils'

apply from: "${rootProject.projectDir}/gradle/publishing.gradle"

repositories {
  mavenCentral()
}

java {
  sourceCompatibility = JavaVersion.VERSION_1_8
  targetCompatibility = JavaVersion.VERSION_1_8
}

def junitJupiterVersion = '5.8.1'

dependencies {
  api "com.typesafe:config:1.4.1"
  implementation 'net.bytebuddy:byte-buddy:1.12.1'
  testImplementation("org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}")
  testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}")
}

spotless {
  java {
    licenseHeaderFile 'HEADER'
    googleJavaFormat("1.7")
  }
}

spotbugsMain {
  effort = 'max'
  reportLevel = 'low'
  reports {
    xml {
      required = false
    }
    html {
      required = true
    }
  }
}

gradle.projectsEvaluated {
  tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
  }
}

release {
  tagTemplate = 'v$version'
}

test {
  useJUnitPlatform()
}

jacocoTestReport {
  reports {
    xml {
      required = true
    }
  }
}

sonarqube {
  properties {
    property 'sonar.host.url', 'https://sonarcloud.io'
    property 'sonar.organization', 'datadriven'
    property "sonar.projectKey", "datadrivencz_config-view"
  }
}
